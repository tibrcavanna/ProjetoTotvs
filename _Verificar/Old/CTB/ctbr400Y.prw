#Include "CTBR400Y.Ch"
#Include "PROTHEUS.Ch"
/*/


Ŀ
Funo     CTBR400Y   Autor  Pilar S. Albaladejo    Data  05.02.01 
Ĵ
Descrio  Emisso do Razo                                           
Ĵ
Sintaxe    CTBR400()                                                  
Ĵ
Retorno    Nenhum                                                     
Ĵ
 Uso       Generico                                                   
Ĵ
Parametros Nenhum                                                     
ٱ


/*/
User Function CTBR400Y()

Local aCtbMoeda	:= {}
Local WnRel			:= "CTBR400Y"
Local cDesc1		:= STR0001	//"Este programa ir imprimir o Razo Contabil,"
Local cDesc2		:= STR0002	// "de acordo com os parametros solicitados pelo"
Local cDesc3		:= STR0003	// "usuario."
Local cString		:= "CT2"
Local titulo		:= STR0006 	//"Emissao do Razao Contabil"
Local lCusto		:= .F.
Local lItem			:= .F.
Local lCLVL			:= .F.                         
Local lAnalitico 	:= .T.
Local lRet			:= .T.
Local nTamLinha		:= 132

Private aReturn	:= { STR0004, 1,STR0005, 2, 2, 1, "", 1 }  //"Zebrado"###"Administracao"
Private nomeprog	:= "CTBR400Y"
Private aLinha		:= {}
Private nLastKey	:= 0
Private cPerg		:= "CTR400"
Private Tamanho 		:= "M"

If ( !AMIIn(34) )		// Acesso somente pelo SIGACTB
	Return
EndIf

If ! Pergunte("CTR400", .T. )
	Return
Endif
//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01            // da conta                              
// mv_par02            // ate a conta                           
// mv_par03            // da data                               
// mv_par04            // Ate a data                            
// mv_par05            // Moeda			                        
// mv_par06            // Saldos		                            
// mv_par07            // Set Of Books                          
// mv_par08            // Analitico ou Resumido dia (resumo)    
// mv_par09            // Imprime conta sem movimento?          
// mv_par10            // Junta Contas com mesmo C.Custo?       
// mv_par11            // Imprime Cod (Normal / Reduzida)       
// mv_par12            // Imprime C.Custo?                      
// mv_par13            // Do Centro de Custo                    
// mv_par14            // At o Centro de Custo                 
// mv_par15            // Imprime Item?	                     	
// mv_par16            // Do Item                               
// mv_par17            // Ate Item                              
// mv_par18            // Imprime Classe de Valor?              	
// mv_par19            // Da Classe de Valor                    
// mv_par20            // Ate a Classe de Valor                 
// mv_par21            // Salto de pagina                       
// mv_par22            // Pagina Inicial                        
// mv_par23            // Pagina Final                          
// mv_par24            // Numero da Pag p/ Reiniciar            	   
// mv_par25            // Imprime Cod C.Custo(Normal / Reduzido)
// mv_par26            // Imprime Cod Item (Normal / Reduzido)  
// mv_par27            // Imprime Cod Cl.Valor(Normal /Reduzida)
// mv_par28            // Imprime Total Geral (Sim/Nao)         
// mv_par29            // So Livro/Livro e Termos/So Termos     
//

lCusto 		:= Iif(mv_par12 == 1,.T.,.F.)
lItem		:= Iif(mv_par15 == 1,.T.,.F.)
lCLVL		:= Iif(mv_par18 == 1,.T.,.F.)
lAnalitico	:= Iif(mv_par08 == 1,.T.,.F.)

If (lCusto .Or. lItem .Or. lCLVL) .And. lAnalitico
	Tamanho 	:= "G"
	nTamLinha	:= 220
EndIf	

wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"")

lCusto 		:= Iif(mv_par12 == 1,.T.,.F.)
lItem		:= Iif(mv_par15 == 1,.T.,.F.)
lCLVL		:= Iif(mv_par18 == 1,.T.,.F.)
lAnalitico	:= Iif(mv_par08 == 1,.T.,.F.)


If (lCusto .Or. lItem .Or. lCLVL) .And. lAnalitico
	Tamanho 	:= "G"
	nTamLinha	:= 220
Else
	nTamLinha	:= 132
	Tamanho 	:= "M"
EndIf	

If nLastKey = 27
	Set Filter To
	Return
Endif

//Ŀ
// Verifica se usa Set Of Books -> Conf. da Mascara / Valores   
//
If !Ct040Valid(mv_par07)
	lRet := .F.
Else
	aSetOfBook := CTBSetOf(mv_par07)
EndIf

If lRet
	aCtbMoeda  	:= CtbMoeda(mv_par05)
   If Empty(aCtbMoeda[1])
      Help(" ",1,"NOMOEDA")
      lRet := .F.
   Endif
Endif

If !lRet	
	Set Filter To
	Return
EndIf

SetDefault(aReturn,cString)

If nLastKey = 27
	Set Filter To
	Return
Endif

RptStatus({|lEnd| CTR400Imp(@lEnd,wnRel,cString,aSetOfBook,lCusto,lItem,lCLVL,;
		lAnalitico,Titulo,nTamlinha,aCtbMoeda)})
Return 

/*/


Ŀ
 Funo    CTR400Imp  Autor  Pilar S. Albaladejo    Data  05/02/01 
Ĵ
 Descrio  Impressao do Razao                                         
Ĵ
 Sintaxe   Ctr400Imp(lEnd,wnRel,cString,aSetOfBook,lCusto,lItem,;      
                     lCLVL,Titulo,nTamLinha,aCtbMoeda)                 
Ĵ
 Retorno   Nenhum                                                      
Ĵ
 Uso        SIGACTB                                                    
Ĵ
Parametros  lEnd       - Aao do Codeblock                             
            wnRel      - Nome do Relatorio                             
            cString    - Mensagem                                      
            aSetOfBook - Array de configuracao set of book             
            lCusto     - Imprime Centro de Custo?                      
            lItem      - Imprime Item Contabil?                        
            lCLVL      - Imprime Classe de Valor?                       
            Titulo     - Titulo do Relatorio                           
            nTamLinha  - Tamanho da linha a ser impressa                
            aCtbMoeda  - Moeda                                         
ٱ


/*/
Static Function CTR400Imp(lEnd,WnRel,cString,aSetOfBook,lCusto,lItem,lCLVL,lAnalitico,Titulo,nTamlinha,;
						aCtbMoeda)

//Ŀ
// Define Variaveis                                             
//
Local CbTxt
Local cbcont
Local Cabec1		:= ""
Local Cabec2		:= ""

Local aSaldo		:= {}
Local cDescMoeda
Local cMascara1
Local cMascara2
Local cMascara3
Local cMascara4
Local cPicture
Local cSepara1		:= ""
Local cSepara2		:= ""
Local cSepara3		:= ""
Local cSepara4		:= ""
Local cSaldo		:= mv_par06
Local cContaIni		:= mv_par01
Local cContaFIm		:= mv_par02
Local cCustoIni		:= mv_par13
Local cCustoFim		:= mv_par14
Local cItemIni		:= mv_par16
Local cItemFim		:= mv_par17
Local cCLVLIni		:= mv_par19
Local cCLVLFim		:= mv_par20
Local cContaAnt		:= ""
Local dDataAnt		:= CTOD("  /  /  ")
Local cDescConta	:= ""
Local cCodRes		:= ""
Local cResCC		:= ""
Local cResItem		:= ""
Local cResCLVL		:= ""
Local cDescSint		:= ""
Local cMoeda		:= mv_par05
Local cContaSint	:= ""
Local cArqTmp
Local dDataIni		:= mv_par03
Local dDataFim		:= mv_par04
Local lNoMov		:= Iif(mv_par09==1,.T.,.F.)
Local lJunta		:= Iif(mv_par10==1,.T.,.F.)
Local lSalto		:= Iif(mv_par21==1,.T.,.F.)
Local lFirst		:= .T.
Local nDecimais
Local nTotDeb		:= 0
Local nTotCrd		:= 0
Local nTotGerDeb	:= 0
Local nTotGerCrd	:= 0
Local nReinicia 	:= mv_par24
Local nPagFim		:= mv_par23
Local nTamConta		:= 20
Local nVlrDeb		:= 0
Local nVlrCrd		:= 0, aColunas, l132 := .F.
Local lImpLivro		:=.t., lImpTermos:=.f.								

//Ŀ
// Impressao de Termo / Livro                                   
//
Do Case
	Case mv_par29==1 ; lImpLivro:=.t. ; lImpTermos:=.f.
	Case mv_par29==2 ; lImpLivro:=.t. ; lImpTermos:=.t.
	Case mv_par29==3 ; lImpLivro:=.f. ; lImpTermos:=.t.
EndCase		

//Ŀ
// Variaveis utilizadas para Impressao do Cabecalho e Rodape    
//
cbtxt    := SPACE(10)
cbcont   := 0
li       := 80
m_pag    := 1

cDescMoeda 	:= Alltrim(aCtbMoeda[2])
nDecimais 	:= DecimalCTB(aSetOfBook,cMoeda)

// Mascara da Conta
If Empty(aSetOfBook[2])
	cMascara1 := GetMv("MV_MASCARA")
Else
	cMascara1	:= RetMasCtb(aSetOfBook[2],@cSepara1)
EndIf               

If lCusto .Or. lItem .Or. lCLVL
	// Mascara do Centro de Custo
	If Empty(aSetOfBook[6])
		cMascara2 := GetMv("MV_MASCCUS")
	Else
		cMascara2	:= RetMasCtb(aSetOfBook[6],@cSepara2)
	EndIf                                                
	// Mascara do Item Contabil
	If Empty(aSetOfBook[7])
		cMascara3 := ""
	Else
		cMascara3 := RetMasCtb(aSetOfBook[7],@cSepara3)
	EndIf
	// Mascara da Classe de Valor
	If Empty(aSetOfBook[8])
		cMascara4 := ""
	Else
		cMascara4 := RetMasCtb(aSetOfBook[8],@cSepara4)
	EndIf
EndIf	

cPicture 	:= aSetOfBook[4]

//Ŀ
//Titulo do Relatorio                                                       
//
If Type("NewHead")== "U"
	IF lAnalitico
		Titulo	:=	STR0007	//"RAZAO ANALITICO EM "
	Else
		Titulo	:=	STR0008	//"RAZAO SINTETICO EM "
	EndIf
	Titulo += 	cDescMoeda + STR0009 + DTOC(dDataIni) +;	// "DE"
				STR0010 + DTOC(dDataFim) + CtbTitSaldo(mv_par06)	// "ATE"
Else
	Titulo := NewHead
EndIf

//Ŀ
//Resumido                                  						         
//
// DATA                         					                                DEBITO               CREDITO            SALDO ATUAL
// XX/XX/XXXX 			                                 		     99,999,999,999,999.99 99,999,999,999,999.99 99,999,999,999,999.99D
// 01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
//           1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16         17        18        19        20       21        22
//Ŀ
//Cabealho Conta                                                           
//
// DATA
// LOTE/SUB/DOC/LINHA H I S T O R I C O                        C/PARTIDA                      DEBITO          CREDITO       SALDO ATUAL"
// XX/XX/XXXX         
// XXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXX 9999999999999.99 9999999999999.99 9999999999999.99D
// 012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
//           1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16    
//Ŀ
//Cabealho Conta + CCusto + Item + Classe de Valor								  
//
// DATA
// LOTE/SUB/DOC/LINHA  H I S T O R I C O                        C/PARTIDA                      CENTRO CUSTO         ITEM                 CLASSE DE VALOR                     DEBITO               CREDITO           SALDO ATUAL"
// XX/XX/XXXX 
// XXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXX 99,999,999,999,999.99 99,999,999,999,999.99 99,999,999,999,999.99D
// 01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
//           1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16         17        18        19        20       21        22

#DEFINE 	COL_NUMERO 			1
#DEFINE 	COL_HISTORICO		2
#DEFINE 	COL_CONTRA_PARTIDA	3
#DEFINE 	COL_CENTRO_CUSTO 	4
#DEFINE 	COL_ITEM_CONTABIL 	5
#DEFINE 	COL_CLASSE_VALOR  	6 
#DEFINE 	COL_VLR_DEBITO		7
#DEFINE 	COL_VLR_CREDITO		8
#DEFINE 	COL_VLR_SALDO  		9
#DEFINE 	TAMANHO_TM       	10
#DEFINE 	COL_VLR_TRANSPORTE  11

If ! lAnalitico
	l132 := .T.
	aColunas := { 000, 019,    ,    ,    ,    , 069, 091, 112, 19, 091 }
ElseIf ! lItem .And. ! lCLVL .And. ! lCusto
	l132 := .T.
	aColunas := { 000, 019, 060,    ,    ,    , 081, 098, 114, 16 ,098 }
Else
	aColunas := { 000, 019, 060, 091, 112, 133, 154, 176, 197, 21 ,176 }
Endif

If lAnalitico								// Relatorio Analitico
	Cabec1 := STR0019						// "DATA"
	If lCusto .Or. lItem .Or. lCLVL
		Cabec2 := STR0013			  		// "LOTE/SUB/DOC/LINHA  H I S T O R I C O                        C/PARTIDA            CENTRO CUSTO         ITEM                 CLASSE DE VALOR                     DEBITO               CREDITO           SALDO ATUAL"
	Else	                                    	
		Cabec2 := STR0014 					// "LOTE/SUB/DOC/LINHA  H I S T O R I C O                        C/PARTIDA                       DEBITO           CREDITO       SALDO ATUAL"
	EndIf	     
Else                
	lCusto := .F.
	lItem  := .F.
	lCLVL  := .F.
	Cabec1 := STR0024						// "DATA					                              					              	 DEBITO               CREDITO          	SALDO ATUAL"
EndIf	

m_pag := mv_par22

If lImpLivro
	//Ŀ
	// Monta Arquivo Temporario para Impressao   					 
	//
	MsgMeter({|	oMeter, oText, oDlg, lEnd | ;
				CTBGerRaz(oMeter,oText,oDlg,lEnd,@cArqTmp,cContaIni,cContaFim,cCustoIni,cCustoFim,;
				cItemIni,cItemFim,cCLVLIni,cCLVLFim,cMoeda,dDataIni,dDataFim,;
				aSetOfBook,lNoMov,cSaldo,lJunta,"1",lAnalitico)},;
				STR0018,;		// "Criando Arquivo Temporrio..."
				STR0006)		// "Emissao do Razao"

	dbSelectArea("cArqTmp")
	SetRegua(RecCount())
	dbGoTop()
Endif

While lImpLivro .And. !Eof()

	IF lEnd
		@Prow()+1,0 PSAY STR0015  //"***** CANCELADO PELO OPERADOR *****"
		Exit
	EndIF

	IncRegua()

	aSaldo := SaldoCT7(cArqTmp->CONTA,cArqTmp->DATAL,cMoeda,cSaldo)
	
	If lNoMov //Se imprime conta sem movimento
		If aSaldo[6] == 0 .And. cArqTmp->LANCDEB ==0 .And. cArqTmp->LANCCRD == 0 
			dbSelectArea("cArqTmp")
			dbSkip()
			Loop
		Endif	
	Endif             

	If li > 56 .Or. lSalto              
		If m_pag > nPagFim
			m_pag := nReinicia
		EndIf	
		CtCGCCabec(lItem,lCusto,lCLVL,Cabec1,Cabec2,dDataFim,Titulo,lAnalitico,"1")
	EndIf

	nSaldoAtu:= 0
	nTotDeb	:= 0
	nTotCrd	:= 0
                              
	// IMPRIME A CONTA
	
	// Conta Sintetica	
	cContaSint := Ctr400Sint(cArqTmp->CONTA,@cDescSint,cMoeda,@cDescConta,@cCodRes)
	EntidadeCTB(cContaSint,li,001,20,.F.,cMascara1,cSepara1) 
	@li,025 PSAY " - " + cDescSint
	li+=2
	
	// Conta Analitica


	@li,011 PSAY STR0016 	//"CONTA - "	

	If lCusto .Or. lItem .Or. lCLVL
		nTamConta := 30						// Tamanho disponivel no relatorio para imprimir
	Else											//o codigo da conta
		nTamConta := 20
	EndIf		
	If mv_par11 == 1							// Imprime Cod Normal
		EntidadeCTB(cArqTmp->CONTA,li,20,nTamConta,.F.,cMascara1,cSepara1)
	Else
		EntidadeCTB(cCodRes,li,20,nTamConta,.F.,cMascara1,cSepara1)
	EndIf
	@ li, (20 + nTamConta) PSAY "- " + cDescConta
	
	// Impressao do Saldo Anterior do Centro de Custo
	ValorCTB(aSaldo[6],li,aColunas[COL_VLR_SALDO],aColunas[TAMANHO_TM],nDecimais,;
							         .T.,cPicture)
		
	nSaldoAtu := aSaldo[6]                                           
	li += 2         
	dbSelectArea("cArqTmp")
	cContaAnt:= cArqTmp->CONTA
	dDataAnt	:= CTOD("  /  /  ")
	While !Eof() .And. cArqTmp->CONTA == cContaAnt
	
		If li > 56
			If m_pag > nPagFim
				m_pag := nReinicia
			EndIf	
			li++
			
			@li,aColunas[COL_VLR_TRANSPORTE] - Len(STR0022) - 1;
						 PSAY STR0022	//"A TRANSPORTAR : "
			ValorCTB(nSaldoAtu,li,aColunas[COL_VLR_SALDO],;
								   aColunas[TAMANHO_TM],nDecimais, .T.,cPicture)
			
			CtCGCCabec(lItem,lCusto,lCLVL,Cabec1,Cabec2,dDataFim,Titulo,lAnalitico,"1")
			
			@li,aColunas[COL_VLR_TRANSPORTE] - Len(STR0023) - 1;
						 PSAY STR0023	//"A TRANSPORTAR : "
			ValorCTB(nSaldoAtu,li,aColunas[COL_VLR_SALDO],;
								   aColunas[TAMANHO_TM],nDecimais, .T.,cPicture)
			li++
		EndIf
	
		// Imprime os lancamentos para a conta                          
		
		If dDataAnt != cArqTmp->DATAL  .And. (cArqTmp->LANCDEB <> 0 .Or. cArqTmp->LANCCRD <> 0) 
			If lAnalitico
				@li,000 PSAY cArqTmp->DATAL
				li++                       
				dDataAnt := cArqTmp->DATAL
			Else
				@li,000 PSAY cArqTmp->DATAL
				dDataAnt := cArqTmp->DATAL	
			Endif		
		EndIf	
		
		If lAnalitico		//Se for relatorio analitico
			nSaldoAtu 	:= nSaldoAtu - cArqTmp->LANCDEB + cArqTmp->LANCCRD
			nTotDeb		+= cArqTmp->LANCDEB
			nTotCrd		+= cArqTmp->LANCCRD
			nTotGerDeb	+= cArqTmp->LANCDEB
			nTotGerCrd	+= cArqTmp->LANCCRD			
			
			dbSelectArea("CT1")
			dbSetOrder(1)
			dbSeek(xFilial()+cArqTmp->XPARTIDA)
			cCodRes := CT1->CT1_RES
			dbSelectArea("cArqTmp")

			@li,aColunas[COL_NUMERO] PSAY cArqTmp->LOTE+cArqTmp->SUBLOTE+;
										   cArqTmp->DOC+cArqTmp->LINHA
			@li,aColunas[COL_HISTORICO] PSAY cArqTmp->HISTORICO                        
			dbSelectArea("CT1")
			dbSetOrder(1)
			dbSeek(xFilial()+cArqTmp->XPARTIDA)
			cCodRes := CT1->CT1_RES
			dbSelectArea("cArqTmp")

			If mv_par11 == 1
				EntidadeCTB(cArqTmp->XPARTIDA,li,aColunas[COL_CONTRA_PARTIDA],;
							20,.F.,cMascara1,cSepara1)
			Else
				EntidadeCTB(cCodRes,li,aColunas[COL_CONTRA_PARTIDA],20,.F.,;
							cMascara1,cSepara1)				
			Endif                              

			If lCusto
				If mv_par25 == 1 //Imprime Cod. Centro de Custo Normal 
					EntidadeCTB(cArqTmp->CCUSTO,li,aColunas[COL_CENTRO_CUSTO],20,.F.,cMascara2,cSepara2)
				Else 
					dbSelectArea("CTT")
					dbSetOrder(1)
					dbSeek(xFilial()+cArqTmp->CCUSTO)				
					cResCC := CTT->CTT_RES
					EntidadeCTB(cResCC,li,aColunas[COL_CENTRO_CUSTO],20,.F.,cMascara2,cSepara2)
					dbSelectArea("cArqTmp")
				Endif                                                       
			Endif

			If lItem 						//Se imprime item 
				If mv_par25 == 1 //Imprime Codigo Normal Item Contabl
					EntidadeCTB(cArqTmp->ITEM,li,aColunas[COL_ITEM_CONTABIL],20,.F.,cMascara3,cSepara3)
				Else
					dbSelectArea("CTD")
					dbSetOrder(1)
					dbSeek(xFilial()+cArqTmp->ITEM)				
					cResItem := CTD->CTD_RES
					EntidadeCTB(cResItem,li,aColunas[COL_ITEM_CONTABIL],20,.F.,cMascara3,cSepara3)						
					dbSelectArea("cArqTmp")					
				Endif
			Endif
				
			If lCLVL						//Se imprime classe de valor
				If mv_par26 == 1 //Imprime Cod. Normal Classe de Valor
					EntidadeCTB(cArqTmp->CLVL,li,aColunas[COL_CLASSE_VALOR],20,.F.,cMascara4,cSepara4)
				Else
					dbSelectArea("CTH")
					dbSetOrder(1)
					dbSeek(xFilial()+cArqTmp->CLVL)				
					cResClVl := CTH->CTH_RES						
					EntidadeCTB(cResClVl,li,aColunas[COL_CLASSE_VALOR],20,.F.,cMascara4,cSepara4)
					dbSelectArea("cArqTmp")					
				Endif			
			Endif
			
			ValorCTB(cArqTmp->LANCDEB,li,aColunas[COL_VLR_DEBITO],;
										  aColunas[TAMANHO_TM],nDecimais,.F.,cPicture)
			ValorCTB(cArqTmp->LANCCRD,li,aColunas[COL_VLR_CREDITO],;
										  aColunas[TAMANHO_TM],nDecimais,.F.,cPicture)
			ValorCTB(nSaldoAtu,li,aColunas[COL_VLR_SALDO],;
								   aColunas[TAMANHO_TM],nDecimais,.T.,cPicture)
		
			// Procura pelo complemento de historico
			dbSelectArea("CT2")
			dbSetOrder(10)
			If dbSeek(xFilial()+DTOS(cArqTMP->DATAL)+cArqTmp->LOTE+cArqTmp->SUBLOTE+;
													   cArqTmp->DOC+cArqTmp->SEQLAN)
				dbSkip()
				If CT2->CT2_DC == "4"
					While !Eof() .And. CT2->CT2_FILIAL == xFilial() 			.And.;
										CT2->CT2_LOTE == cArqTMP->LOTE 		.And.;
										CT2->CT2_SBLOTE == cArqTMP->SUBLOTE 	.And.;
										CT2->CT2_DOC == cArqTmp->DOC 			.And.;
										CT2->CT2_SEQLAN == cArqTmp->SEQLAN 	.And.;
										CT2->CT2_DC == "4" 					.And.;
								   DTOS(CT2->CT2_DATA) == DTOS(cArqTmp->DATAL)                        
						li++
						@li,aColunas[COL_NUMERO] 	 PSAY Space(15)+CT2->CT2_LINHA
						@li,aColunas[COL_HISTORICO] PSAY CT2->CT2_HIST
						dbSkip()
					EndDo	
				EndIf	
			EndIf	
			dbSelectArea("cArqTmp")
			dbSkip()			
		Else		// Se for resumido.                               			
			While dDataAnt == cArqTmp->DATAL .And. cContaAnt == cArqTmp->CONTA
				nVlrDeb	+= cArqTmp->LANCDEB		                                         
				nVlrCrd	+= cArqTmp->LANCCRD		                                         
				nTotGerDeb	+= cArqTmp->LANCDEB
				nTotGerCrd	+= cArqTmp->LANCCRD			
				dbSkip()                                                                    				
			End			                                                                    
			nSaldoAtu	:= nSaldoAtu - nVlrDeb + nVlrCrd
			ValorCTB(nVlrDeb,li,aColunas[COL_VLR_DEBITO],aColunas[TAMANHO_TM],;
					 nDecimais,.F.,cPicture)
			ValorCTB(nVlrCrd,li,aColunas[COL_VLR_CREDITO],aColunas[TAMANHO_TM],;
					 nDecimais,.F.,cPicture)
			ValorCTB(nSaldoAtu,li,aColunas[COL_VLR_SALDO],aColunas[TAMANHO_TM],;
					 nDecimais,.T.,cPicture)
			nTotDeb		+= nVlrDeb
			nTotCrd		+= nVlrCrd         
			nVlrDeb	:= 0
			nVlrCrd	:= 0
		Endif
		dbSelectArea("cArqTmp")
		//dbSkip()  
		li++
	EndDo

    li+=2
	If li > 56
		If m_pag > nPagFim
			m_pag := nReinicia
		EndIf	
		li++
		@li,aColunas[COL_VLR_TRANSPORTE] - Len(STR0022) - 1;
					 PSAY STR0022	//"A TRANSPORTAR : "
		ValorCTB(nSaldoAtu,li,aColunas[COL_VLR_SALDO],;
							   aColunas[TAMANHO_TM],nDecimais, .T.,cPicture)
		
		CtCGCCabec(lItem,lCusto,lCLVL,Cabec1,Cabec2,dDataFim,Titulo,lAnalitico,"1")
		
		@li,aColunas[COL_VLR_TRANSPORTE] - Len(STR0023) - 1;
				 PSAY STR0023	//"A TRANSPORTAR : "
		ValorCTB(nSaldoAtu,li,aColunas[COL_VLR_SALDO],;
							   aColunas[TAMANHO_TM],nDecimais, .T.,cPicture)
		li++
    EndIf
    
	If l132
		@li,000 PSAY STR0020  //"T o t a i s  d a  C o n t a  ==> " 	    
	Else
		@li,aColunas[COL_HISTORICO] PSAY STR0020  //"T o t a i s  d a  C o n t a  ==> " 	    
	Endif			

	ValorCTB(nTotDeb,li,aColunas[COL_VLR_DEBITO],aColunas[TAMANHO_TM],nDecimais,;
			 .F.,cPicture)
	ValorCTB(nTotCrd,li,aColunas[COL_VLR_CREDITO],aColunas[TAMANHO_TM],nDecimais,;
			 .F.,cPicture)
	ValorCTB(nSaldoAtu,li,aColunas[COL_VLR_SALDO],aColunas[TAMANHO_TM],nDecimais,;
			 .T.,cPicture)
    
	li++
	@li, 00 PSAY Replicate("-",nTamLinha)
	li++

EndDo	 
          
If lImpLivro .And. mv_par28 == 1	//Imprime total Geral
    @li, 30 PSAY STR0025  //"T O T A L  G E R A L  ==> " 	        
	If lAnalitico .And. (lCusto .Or. lItem .Or. lClVl)
		ValorCTB(nTotGerDeb,li,aColunas[COL_VLR_DEBITO],aColunas[TAMANHO_TM],nDecimais,.F.,cPicture)
		ValorCTB(nTotGerCrd,li,aColunas[COL_VLR_CREDITO],aColunas[TAMANHO_TM],nDecimais,.F.,cPicture)
		li++
		@li, 00 PSAY Replicate("-",nTamLinha)
		li+=2		
	Else
		ValorCTB(nTotGerDeb,li,aColunas[COL_VLR_DEBITO],aColunas[TAMANHO_TM],nDecimais,.F.,cPicture)
		ValorCTB(nTotGerCrd,li,aColunas[COL_VLR_CREDITO],aColunas[TAMANHO_TM],nDecimais,.F.,cPicture)
		li++
		@li, 00 PSAY Replicate("-",nTamLinha)
		li+=2
	Endif
	roda(cbcont,cbtxt,Tamanho)
	Set Filter To
Endif

If lImpTermos 							// Impressao dos Termos

	cArqAbert:=GetNewPar("MV_LRAZABE","")
	cArqEncer:=GetNewPar("MV_LRAZENC","")
	
    If Empty(cArqAbert)
		ApMsgAlert(	"Devem ser criados os parametros MV_LRAZABE e MV_LRAZENC. " +;
					"Utilize como base o parametro MV_LDIARAB.")
	Endif
Endif

If lImpTermos .And. ! Empty(cArqAbert)	// Impressao dos Termos

	dbSelectArea("SM0")
	aVariaveis:={}

	For i:=1 to FCount()	
		If FieldName(i)=="M0_CGC"
			AADD(aVariaveis,{FieldName(i),Transform(FieldGet(i),"@R 99.999.999/9999-99")})
		Else
            If FieldName(i)=="M0_NOME"
                Loop
            EndIf
			AADD(aVariaveis,{FieldName(i),FieldGet(i)})
		Endif
	Next

	dbSelectArea("SX1")
	dbSeek("CTR400"+"01")

	While SX1->X1_GRUPO=="CTR400"
		AADD(aVariaveis,{Rtrim(Upper(X1_VAR01)),&(X1_VAR01)})
		dbSkip()
	End

	If !File(cArqAbert)
		aSavSet:=__SetSets()
		cArqAbert:=CFGX024(,"Razo") // Editor de Termos de Livros
		__SetSets(aSavSet)
		Set(24,Set(24),.t.)
	Endif

	If !File(cArqEncer)
		aSavSet:=__SetSets()
		cArqEncer:=CFGX024(,"Razo") // Editor de Termos de Livros
		__SetSets(aSavSet)
		Set(24,Set(24),.t.)
	Endif

	If cArqAbert#NIL
		ImpTerm(cArqAbert,aVariaveis,AvalImp(132))
	Endif

	If cArqEncer#NIL
		ImpTerm(cArqEncer,aVariaveis,AvalImp(132))
	Endif	 
Endif

If aReturn[5] = 1
	Set Printer To
	Commit
	Ourspool(wnrel)
End

If lImpLivro
	dbSelectArea("cArqTmp")
	Set Filter To
	dbCloseArea()
	If Select("cArqTmp") == 0
		FErase(cArqTmp+GetDBExtension())
		FErase(cArqTmp+OrdBagExt())
	EndIf	
Endif

dbselectArea("CT2")

MS_FLUSH()

Return

/*/


Ŀ
 Funo    CtbGerRaz  Autor  Pilar S. Albaladejo    Data  05/02/01 
Ĵ
 Descrio Cria Arquivo Temporario para imprimir o Razao               
Ĵ
 Sintaxe   CtbGerRaz(oMeter,oText,oDlg,lEnd,cArqTmp,cContaIni,cContaFim
			  cCustoIni,cCustoFim,cItemIni,cItemFim,cCLVLIni,cCLVLFim,    
			  cMoeda,dDataIni,dDataFim,aSetOfBook,lNoMov,cSaldo,lJunta,   
			  cTipo,lAnalit)                                              
Ĵ
Retorno    Nome do arquivo temporario                                  
Ĵ
 Uso        SIGACTB                                                    
Ĵ
Parametros  ExpO1 = Objeto oMeter                                      
            ExpO2 = Objeto oText                                       
            ExpO3 = Objeto oDlg                                        
            ExpL1 = Acao do Codeblock                                  
            ExpC1 = Arquivo temporario                                 
            ExpC2 = Conta Inicial                                      
            ExpC3 = Conta Final                                        
            ExpC4 = C.Custo Inicial                                    
            ExpC5 = C.Custo Final                                      
            ExpC6 = Item Inicial                                       
            ExpC7 = Cl.Valor Inicial                                   
            ExpC8 = Cl.Valor Final                                     
            ExpC9 = Moeda                                              
            ExpD1 = Data Inicial                                       
            ExpD2 = Data Final                                         
            ExpA1 = Matriz aSetOfBook                                  
            ExpL2 = Indica se imprime movimento zerado ou nao.         
            ExpC10= Tipo de Saldo                                      
            ExpL3 = Indica se junta CC ou nao.                         
            ExpC11= Tipo do lancamento                                 
            ExpL4 = Indica se imprime analitico ou sintetico           
            c2Moeda = Indica moeda 2 a ser incluida no relatorio       
ٱ


/*/
Static Function CtbGerRaz(oMeter,oText,oDlg,lEnd,cArqTmp,cContaIni,cContaFim,cCustoIni,cCustoFim,;
						cItemIni,cItemFim,cCLVLIni,cCLVLFim,cMoeda,dDataIni,dDataFim,;
						aSetOfBook,lNoMov,cSaldo,lJunta,cTipo,lAnalit,c2Moeda)

Local aTamConta	:= TAMSX3("CT1_CONTA")
Local aTamCusto	:= TAMSX3("CT3_CUSTO")
Local aCtbMoeda	:= {}
Local aSaveArea := GetArea()
Local aCampos

Local cChave
Local nTamDesc	:= Len(CriaVar("CT1_DESC"+cMoeda))
Local nTamHist	:= Len(CriaVar("CT2_HIST"))
Local nTamItem	:= Len(CriaVar("CTD_ITEM"))
Local nTamCLVL	:= Len(CriaVar("CTH_CLVL"))
Local nDecimais	:= 0

DEFAULT c2Moeda := ""

// Retorna Decimais
aCtbMoeda := CTbMoeda(cMoeda)
nDecimais := aCtbMoeda[5]

aCampos :={	{ "CONTA"		, "C", aTamConta[1], 0 },;  		// Codigo da Conta
			{ "XPARTIDA"   	, "C", aTamConta[1] , 0 },;		// Contra Partida
			{ "TIPO"       	, "C", 01			, 0 },;			// Tipo do Registro (Debito/Credito/Continuacao)
			{ "LANCDEB"		, "N", 17			, nDecimais },; // Debito
			{ "LANCCRD"		, "N", 17			, nDecimais },; // Credito
			{ "HISTORICO"	, "C", nTamHist   	, 0 },;			// Historico
			{ "CCUSTO"		, "C", aTamCusto[1], 0 },;			// Centro de Custo
			{ "ITEM"		, "C", nTamItem		, 0 },;			// Item Contabil
			{ "CLVL"		, "C", nTamCLVL		, 0 },;			// Classe de Valor
			{ "DATAL"		, "D", 10			, 0 },;			// Data do Lancamento
			{ "LOTE" 		, "C", 06			, 0 },;			// Lote
			{ "SUBLOTE" 	, "C", 03			, 0 },;			// Sub-Lote
			{ "DOC" 		, "C", 06			, 0 },;			// Documento
			{ "LINHA"		, "C", 03			, 0 },;			// Linha
			{ "SEQLAN"		, "C", 03			, 0 },;			// Sequencia do Lancamento
			{ "SEQHIST"		, "C", 03			, 0 },;			// Seq do Historico
			{ "EMPORI"		, "C", 02			, 0 },;			// Empresa Original
			{ "FILORI"		, "C", 02			, 0 },;			// Filial Original
			{ "NOMOV"		, "L", 01			, 0 }}			// Conta Sem Movimento

If ! Empty(c2Moeda)
	Aadd(aCampos, { "LANCDEB_1"	, "N", 17, nDecimais }) // Debito
	Aadd(aCampos, { "LANCCRD_1"	, "N", 17, nDecimais }) // Credito
	Aadd(aCampos, { "TXDEBITO"	, "N", 17, 4 }) // Taxa Debito
	Aadd(aCampos, { "TXCREDITO"	, "N",  7, 4 }) // Taxa Credito
Endif
																	
cArqTmp := CriaTrab(aCampos, .T.)

dbUseArea( .T.,, cArqTmp, "cArqTmp", .F., .F. )

//Ŀ
// Cria Indice Temporario do Arquivo de Trabalho 1.             
//
If cTipo == "1"			// Razao por Conta
	cChave   := "CONTA+DTOS(DATAL)+LOTE+SUBLOTE+DOC+LINHA+EMPORI+FILORI"
ElseIf cTipo == "2"		// Razao por Centro de Custo                   
	If lAnalit 				// Se o relatorio for analitico
		cChave 	:= "CCUSTO+CONTA+DTOS(DATAL)+LOTE+SUBLOTE+DOC+LINHA+EMPORI+FILORI"
	Else                                                                  
		cChave 	:= "CCUSTO+DTOS(DATAL)+LOTE+SUBLOTE+DOC+LINHA+EMPORI+FILORI"
	Endif
ElseIf cTipo == "3" 		//Razao por Item Contabil      
	If lAnalit 				// Se o relatorio for analitico               
		cChave 	:= "ITEM+CONTA+DTOS(DATAL)+LOTE+SUBLOTE+DOC+LINHA+EMPORI+FILORI"
	Else                                                                  
		cChave 	:= "ITEM+DTOS(DATAL)+LOTE+SUBLOTE+DOC+LINHA+EMPORI+FILORI"
	Endif
ElseIf cTipo == "4"		//Razao por Classe de Valor	
	If lAnalit 				// Se o relatorio for analitico               
		cChave 	:= "CLVL+CONTA+DTOS(DATAL)+LOTE+SUBLOTE+DOC+LINHA+EMPORI+FILORI"
	Else                                                                  
		cChave 	:= "CLVL+DTOS(DATAL)+LOTE+SUBLOTE+DOC+LINHA+EMPORI+FILORI"
	Endif	
EndIf

IndRegua("cArqTmp",cArqTmp,cChave,,,STR0017)  //"Selecionando Registros..."
dbSelectArea("cArqTmp")
dbSetIndex(cArqTmp+OrdBagExt())
dbSetOrder(1)

// Monta Arquivo para gerar o Razao
CtbRazao(oMeter,oText,oDlg,lEnd,cContaIni,cContaFim,cCustoIni,cCustoFim,;
			cItemIni,cItemFim,cCLVLIni,cCLVLFim,cMoeda,dDataIni,dDataFim,;
			aSetOfBook,lNoMov,cSaldo,lJunta,cTipo,c2Moeda)

RestArea(aSaveArea)

Return cArqTmp

/*/


Ŀ
 Funo    CtbRazao   Autor  Pilar S. Albaladejo    Data  05/02/01 
Ĵ
 Descrio Realiza a "filtragem" dos registros do Razao                
Ĵ
Sintaxe    CtbRazao(oMeter,oText,oDlg,lEnd,cContaIni,cContaFim,		   
			  cCustoIni,cCustoFim, cItemIni,cItemFim,cCLVLIni,cCLVLFim,   
			  cMoeda,dDataIni,dDataFim,aSetOfBook,lNoMov,cSaldo,lJunta,   
			  cTipo)                                                      
Ĵ
Retorno    Nenhum                                                      
Ĵ
 Uso        SIGACTB                                                    
Ĵ
Parametros  ExpO1 = Objeto oMeter                                      
            ExpO2 = Objeto oText                                       
            ExpO3 = Objeto oDlg                                        
            ExpL1 = Acao do Codeblock                                  
            ExpC2 = Conta Inicial                                      
            ExpC3 = Conta Final                                        
            ExpC4 = C.Custo Inicial                                    
            ExpC5 = C.Custo Final                                      
            ExpC6 = Item Inicial                                       
            ExpC7 = Cl.Valor Inicial                                   
            ExpC8 = Cl.Valor Final                                     
            ExpC9 = Moeda                                              
            ExpD1 = Data Inicial                                       
            ExpD2 = Data Final                                         
            ExpA1 = Matriz aSetOfBook                                  
            ExpL2 = Indica se imprime movimento zerado ou nao.         
            ExpC10= Tipo de Saldo                                      
            ExpL3 = Indica se junta CC ou nao.                         
            ExpC11= Tipo do lancamento                                 
            c2Moeda = Indica moeda 2 a ser incluida no relatorio       
ٱ


/*/
Static Function CtbRazao(oMeter,oText,oDlg,lEnd,cContaIni,cContaFim,cCustoIni,cCustoFim,;
					  	cItemIni,cItemFim,cCLVLIni,cCLVLFim,cMoeda,dDataIni,dDataFim,;
					  	aSetOfBook,lNoMov,cSaldo,lJunta,cTipo,c2Moeda)

Local aSaveArea 	:= GetArea()
Local lNoMovDeb, lNoMovCrd
Local cChave, cCpoChave, cTmpChave
#IFDEF TOP
Local lCodeBase	:= TcSrvType() = "AS/400"
#Else
Local lCodeBase	:= .T.
#Endif
Local cFilMoeda	:= If(! Empty(c2Moeda), "(CT2_MOEDLC='" + cMoeda + "'.Or." +;
						 	"CT2_MOEDLC='" + c2Moeda + "').And.",;
							"CT2_MOEDLC='" + cMoeda + "'.And.")
If lCodeBase .And. ! Empty(c2Moeda)
	cFilMoeda := "CT2_MOEDLC$'" + cMoeda + "," + c2Moeda + "'.And."
Endif							

cCustoF := CCUSTOFIM
cContaF := CCONTAFIM      
cItemF 	:= CITEMFIM
cClVlF 	:= CCLVLFIM

oMeter:nTotal := CT1->(RecCount())

// Ŀ
//  Obtm os dbitos 
// 

If cTipo <> "1"
	If cTipo = "2" .And. Empty(cCustoIni)
		CTT->(DbSeek(xFilial()))
		cCustoIni := CTT->CTT_CUSTO
	Endif
	If cTipo = "3" .And. Empty(cItemIni)
		CTD->(DbSeek(xFilial()))
		cItemIni := CTD->CTD_ITEM
	Endif
	If cTipo = "4" .And. Empty(cClVlIni)
		CTH->(DbSeek(xFilial()))
		cClVlIni := CTH->CTH_CLVL
	Endif
Endif

If cTipo == "1"
	dbSelectArea("CT2")
	dbSetOrder(2)
	cValid	:= 	"CT2_DEBITO>='" + cContaIni + "'.And." +;
				"CT2_DEBITO<='" + cContaFim + "'"
	cVldEnt := 	"CT2_CCD>='" + cCustoIni + "'.And." +;
				"CT2_CCD<='" + cCustoFim + "'.And." +;
				"CT2_ITEMD>='" + cItemIni + "'.And." +;
				"CT2_ITEMD<='" + cItemFim + "'.And." +;
				"CT2_CLVLDB>='" + cClVlIni + "'.And." +;
				"CT2_CLVLDB<='" + cClVlFim + "'"
ElseIf cTipo == "2"
	dbSelectArea("CT2")
	dbSetOrder(4)
	cValid	:= 	"CT2_CCD >= '" + cCustoIni + "' .And. " +;
				"CT2_CCD <= '" + cCustoFim + "'"
	cVldEnt := 	"CT2_DEBITO >= '" + cContaIni + "' .And. " +;
				"CT2_DEBITO <= '" + cContaFim + "' .And. " +;
				"CT2_ITEMD >= '" + cItemIni + "' .And. " +;
				"CT2_ITEMD <= '" + cItemFim + "' .And. " +;
				"CT2_CLVLDB >= '" + cClVlIni + "' .And. " +;
				"CT2_CLVLDB <= '" + cClVlFim + "'"
ElseIf cTipo == "3"
	dbSelectArea("CT2")
	dbSetOrder(6)
	cValid 	:= 	"CT2_ITEMD >= '" + cItemIni + "' .And. " +;
				"CT2_ITEMD <= '" + cItemFim + "'"
	cVldEnt	:= 	"CT2_DEBITO >= '" + cContaIni + "' .And. " +;
				"CT2_DEBITO <= '" + cContaFim + "' .And. " +;
				"CT2_CCD >= '" + cCustoIni + "' .And. " +;
				"CT2_CCD <= '" + cCustoFim + "' .And. " +;
				"CT2_CLVLDB >= '" + cClVlIni + "' .And. " +;
				"CT2_CLVLDB <= '" + cClVlFim + "'"
ElseIf ctipo == "4"
	dbSelectArea("CT2")
	dbSetOrder(8)
	cValid 	:= 	"CT2_CLVLDB >= '" + cClVlIni + "' .And. " +;
				"CT2_CLVLDB <= '" + cClVlFim + "'"
	cVldEnt	:= 	"CT2_DEBITO >= '" + cContaIni + "' .And. " +;
				"CT2_DEBITO <= '" + cContaFim + "' .And. " +;
				"CT2_CCD >= '" + cCustoIni + "' .And. " +;
				"CT2_CCD <= '" + cCustoFim + "' .And. " +;
				"CT2_ITEMD >= '" + cItemIni + "' .And. " +;
				"CT2_ITEMD <= '" + cItemFim + "'"
EndIf

cAliasQry := SelDados("CT2", 	"CT2_FILIAL='" + xFilial("CT2") + "'.And." +;
								cValid + ".And.DTOS(CT2_DATA)>='" +;
								Dtos(dDataIni) + "'.And.DTOS(CT2_DATA)<= '" +;
								Dtos(dDataFim) + "'.And." + cVldEnt + ".And." +;
								cFilMoeda + "CT2_TPSALD='" + cSaldo +;
								"'.And.(CT2_DC='1'.Or.CT2_DC='3')",,,,,, IndexKey())

DbSelectArea(cAliasQry)
	
While !Eof()
	CarregaSel("CT2")
	If (CT2->CT2_DC = "1" .Or. CT2->CT2_DC = "3") .And. &(cValid) .And. &(cVldEnt)
		CT2->(CtbGrvRAZ(lJunta,cMoeda,cSaldo,"1",c2Moeda,cAliasQry))
	Endif
	dbSelectArea(cAliasQry)
	dbSkip()
EndDo

RemoveSel("CT2")

// Ŀ
//  Obtm os creditos
// 
If cTipo == "1"
	dbSelectArea("CT2")
	dbSetOrder(3)
ElseIf cTipo == "2"
	dbSelectArea("CT2")
	dbSetOrder(5)
ElseIf cTipo == "3"
	dbSelectArea("CT2")
	dbSetOrder(7)
ElseIf cTipo == "4"		
	dbSelectArea("CT2")
	dbSetOrder(9)
EndIf

If cTipo == "1"
	cValid	:= 	"CT2_CREDIT>='" + cContaIni + "'.And." +;
				"CT2_CREDIT<='" + cContaFim + "'"
	cVldEnt :=	"CT2_CCC>='" + cCustoIni + "'.And." +;
				"CT2_CCC<='" + cCustoFim + "'.And." +;
				"CT2_ITEMC>='" + cItemIni + "'.And." +;
				"CT2_ITEMC<='" + cItemFim + "'.And." +;
				"CT2_CLVLCR>='" + cClVlIni + "'.And." +;
				"CT2_CLVLCR<='" + cClVlFim + "'"
ElseIf cTipo == "2"
	cValid 	:= 	"CT2_CCC >= '" + cCustoIni + "' .And. " +;
				"CT2_CCC <= '" + cCustoFim + "'"
	cVldEnt := 	"CT2_CREDIT >= '" + cContaIni + "' .And. " +;
				"CT2_CREDIT <= '" + cContaFim + "' .And. " +;
				"CT2_ITEMC >= '" + cItemIni + "' .And. " +;
				"CT2_ITEMC <= '" + cItemFim + "' .And. " +;
				"CT2_CLVLCR >= '" + cClVlIni + "' .And. " +;
				"CT2_CLVLCR <= '" + cClVlFim + "'"
ElseIf cTipo == "3"
	cValid 	:= 	"CT2_ITEMC >= '" + cItemIni + "' .And. " +;
				"CT2_ITEMC <= '" + cItemFim + "'"
	cVldEnt := 	"CT2_CREDIT >= '" + cContaIni + "' .And. " +;
				"CT2_CREDIT <= '" + cContaFim + "' .And. " +;
				"CT2_CCC >= '" + cCustoIni + "' .And. " +;
				"CT2_CCC <= '" + cCustoFim + "' .And. " +;
				"CT2_CLVLCR >= '" + cClVlIni + "' .And. " +;
				"CT2_CLVLCR <= '" + cClVlFim + "'"
ElseIf cTipo == "4"		
	cValid 	:= 	"CT2_CLVLCR >= '" + cClVlIni + "' .And. " +;
				"CT2_CLVLCR <= '" + cClVlFim + "'"
	cVldEnt := 	"CT2_CREDIT >= '" + cContaIni + "' .And. " +;
				"CT2_CREDIT <= '" + cContaFim + "' .And. " +;
				"CT2_CCC >= '" + cCustoIni + "' .And. " +;
				"CT2_CCC <= '" + cCustoFim + "' .And. " +;
				"CT2_ITEMC >= '" + cItemIni + "' .And. " +;
				"CT2_ITEMC <= '" + cItemFim + "'"
EndIf	

cAliasQry := SelDados(	"CT2", 	"CT2_FILIAL='" + xFilial("CT2") + "'.And." +;
								cValid + ".And.DTOS(CT2_DATA)>='" +;
								Dtos(dDataIni) + "'.And.DTOS(CT2_DATA)<='" +;
								Dtos(dDataFim) + "'.And." + cVldEnt + ".And." +;
								cFilMoeda + "CT2_TPSALD='" + cSaldo +;
								"'.And.(CT2_DC='2'.Or.CT2_DC='3')",,,,,, IndexKey())
While !Eof()
	CarregaSel("CT2")
	If &(cValid) .And. &(cVldEnt)
		CT2->(CtbGrvRAZ(lJunta,cMoeda,cSaldo,"2",c2Moeda,cAliasQry))
	Endif
	dbSelectArea(cAliasQry)
	dbSkip()
EndDo

RemoveSel("CT2")

If lNoMov
	If cTipo == "1"
		dbSelectArea("CT1")
		dbSetOrder(3)
		IndRegua(	Alias(),CriaTrab(nil,.f.),IndexKey(),,;
						"CT1_FILIAL == '" + xFilial() + "' .And. CT1_CONTA <= '" +;
						cContaF + "' .And. CT1_CLASSE = '2'",STR0017)
		cCpoChave := "CT1_CONTA"
		cTmpChave := "CONTA"
	ElseIf cTipo == "2"
		dbSelectArea("CTT")
		dbSetOrder(2)
		IndRegua(	Alias(),CriaTrab(nil,.f.),IndexKey(),,;
						"CTT_FILIAL == '" + xFilial() + "' .And. CTT_CUSTO <= '" +;
						cCUSTOF + "' .And. CTT_CLASSE == '2'",STR0017)
		cCpoChave := "CTT_CUSTO"
		cTmpChave := "CCUSTO"
	ElseIf ctipo == "3"
		dbSelectArea("CTD")
		dbSetOrder(2)
		IndRegua(	Alias(),CriaTrab(nil,.f.),IndexKey(),,;
						"CTD_FILIAL == '" + xFilial() + "' .And. CTD_ITEM <= '" +;
						cITEMF + "' .And. CTD_CLASSE == '2'",STR0017)
		cCpoChave := "CTD_ITEM"
		cTmpChave := "ITEM"
	ElseIf ctipo == "4"
		dbSelectArea("CTH")
		dbSetOrder(2)
		IndRegua(	Alias(),CriaTrab(nil,.f.),IndexKey(),,;
						"CTH_FILIAL == '" + xFilial() + "' .And. CTH_CLVL <= '" +;
						cCLVLF + "' .And. CTH_CLASSE == '2'",STR0017)
		cCpoChave := "CTH_CLVL"
		cTmpChave := "CLVL"
	EndIf

	cAlias := Alias()

	While ! Eof()
		dbSelectArea("cArqTmp")
		If ! DbSeek(&(cAlias + "->" + cCpoChave))
			CtbGrvNoMov(&(cAlias + "->" + cCpoChave),dDataIni,cTmpChave)
		Endif
		DbSelectArea(cAlias)
		DbSkip()
	EndDo

	DbSelectArea(cAlias)
	DbClearFil()
	RetIndex(cAlias)
Endif

Return

/*/


Ŀ
 Funo    CtbGrvRaz  Autor  Pilar S. Albaladejo    Data  05/02/01 
Ĵ
 Descrio Grava registros no arq temporario - Razao                   
Ĵ
Sintaxe    CtbGrvRaz(lJunta,cMoeda,cSaldo,cTipo)                       
Ĵ
Retorno    Nenhum                                                      
Ĵ
 Uso        SIGACTB                                                    
Ĵ
Parametros  ExpL1 = Se Junta CC ou nao                                 
            ExpC1 = Moeda                                              
            ExpC2 = Tipo de saldo                                      
             ExpC3 = Tipo do lancamento                                 
            c2Moeda = Indica moeda 2 a ser incluida no relatorio       
            cAliasQry = Alias com o conteudo selecionado do CT2        
ٱ


/*/
Static Function CtbGrvRAZ(lJunta,cMoeda,cSaldo,cTipo,c2Moeda,cAliasQry)

Local aSaldo := {}
Local cSeqLan
Local cConta
Local cContra
Local cCusto
Local cItem
Local cCLVL
Local nReg

If cTipo == "1"
	cConta 	:= CT2->CT2_DEBITO
	cContra	:= CT2->CT2_CREDIT
	cCusto	:= CT2->CT2_CCD
	cItem		:= CT2->CT2_ITEMD
	cCLVL		:= CT2->CT2_CLVLDB
EndIf	
If cTipo == "2"
	cConta 	:= CT2->CT2_CREDIT
	cContra 	:= CT2->CT2_DEBITO
	cCusto	:= CT2->CT2_CCC
	cItem		:= CT2->CT2_ITEMC
	cCLVL		:= CT2->CT2_CLVLCR
EndIf		           

dbSelectArea("cArqTmp")
dbSetOrder(1)	

RecLock("cArqTmp",.T.)
Replace DATAL		With CT2->CT2_DATA
Replace TIPO		With cTipo
Replace LOTE		With CT2->CT2_LOTE
Replace SUBLOTE	With CT2->CT2_SBLOTE
Replace DOC			With CT2->CT2_DOC
Replace LINHA		With CT2->CT2_LINHA
Replace CONTA		With cConta
Replace XPARTIDA	With cContra
Replace CCUSTO		With cCusto
Replace ITEM		With cItem
Replace CLVL		With cCLVL
Replace HISTORICO	With CT2->CT2_HIST
Replace EMPORI		With CT2->CT2_EMPORI
Replace FILORI		With CT2->CT2_FILORI
Replace SEQHIST	With CT2->CT2_SEQHIST
Replace SEQLAN		With CT2->CT2_SEQLAN
Replace NOMOV		With .F.							// Conta com movimento
If cTipo == "1"
	Replace LANCDEB	With LANCDEB + CT2->CT2_VALOR
EndIf	
If cTipo == "2"
	Replace LANCCRD	With LANCCRD + CT2->CT2_VALOR
EndIf	    
If CT2->CT2_DC == "3"
	Replace TIPO	With cTipo
Else
	Replace TIPO 	With CT2->CT2_DC
EndIf		

If ! Empty(c2Moeda)
	DbSelectArea(cAliasQry)
	DbSkip()
	CarregaSel("CT2")
	dbSelectArea("cArqTmp")
	If CT2->CT2_LOTE = cArqTmp->LOTE 	.And. CT2->CT2_SBLOTE = cArqTmp->SUBLOTE .And.;
	   CT2->CT2_DOC  = cArqTmp->DOC
		If CT2->CT2_MOEDLC = cMoeda	// O registro da segunda moeda normalmente vira depois
			If cTipo == "1"				// por isso gravo o primeiro conteudo que eh da moeda corrente
				Replace LANCDEB_1	With LANCDEB
				Replace LANCDEB  	With CT2->CT2_VALOR
				Replace TXDEBITO  With LANCDEB_1 / LANCDEB
			Else
				Replace LANCCRD_1	With LANCCRD
				Replace LANCCRD  	With CT2->CT2_VALOR
				Replace TXCREDITO With LANCCRD_1 / LANCCRD
			Endif
		Else
			If cTipo == "1"
				Replace LANCDEB_1		With LANCDEB_1 + CT2->CT2_VALOR
				Replace TXDEBITO    	With LANCDEB_1 / LANCDEB
			Endif
			If cTipo == "2"
				Replace LANCCRD_1		With LANCCRD_1 + CT2->CT2_VALOR
				Replace TXCREDITO   	With LANCCRD_1 / LANCCRD
			Endif
		Endif
	Else
		Replace LANCDEB	With 0.00
		Replace LANCCRD	With 0.00
	Endif
Endif
MsUnlock()

Return

/*/


Ŀ
 Funo    CtbGrvNoMov  Autor  Pilar S. Albaladejo  Data  05/02/01 
Ĵ
 Descrio Grava registros no arq temporario sem movimento.            
Ĵ
Sintaxe    CtbGrvNoMov(cConta)                                         
Ĵ
Retorno    Nenhum                                                      
Ĵ
 Uso        SIGACTB                                                    
Ĵ
Parametros  cConteudo = Conteudo a ser gravado no campo chave de acordo
                        com o razao impresso                           
            dDataL = Data para verificacao do movimento da conta       
            cCpoChave = Nome do campo para gravacao no temporario      
ٱ


/*/
Static Function CtbGrvNoMov(cConteudo,dDataL,cCpoTmp)

dbSelectArea("cArqTmp")
dbSetOrder(1)	

RecLock("cArqTmp",.T.)
Replace &(cCpoTmp)	With cConteudo
If cCpoTmp = "CONTA"
	Replace HISTORICO		With STR0021		//"CONTA SEM MOVIMENTO NO PERIODO"
ElseIf cCpoTmp = "CCUSTO"
	Replace HISTORICO		With Upper(AllTrim(CtbSayApro("CTT"))) + " "  + STR0026	//"SEM MOVIMENTO NO PERIODO"
ElseIf cCpoTmp = "ITEM"
	Replace HISTORICO		With Upper(AllTrim(CtbSayApro("CTD"))) + " "  + STR0026	//"SEM MOVIMENTO NO PERIODO"
ElseIf cCpoTmp = "CLVL"
	Replace HISTORICO		With Upper(AllTrim(CtbSayApro("CTH"))) + " "  + STR0026	//"SEM MOVIMENTO NO PERIODO"
Endif
Replace DATAL 			WITH dDataL 
MsUnlock()

Return

/*/


Ŀ
 Funo    Ctr400Sint Autor  Pilar S. Albaladejo    Data  05/02/01 
Ĵ
 Descrio Imprime conta sintetica da conta do razao                   
Ĵ
Sintaxe    Ctr400Sint(cConta,cDescSint,cMoeda,cDescConta,cCodRes)      
Ĵ
Retorno    Conta Sintetic		                                       
Ĵ
 Uso        SIGACTB                                                    
Ĵ
Parametros  ExpC1 = Conta                                              
            ExpC2 = Descricao da Conta Sintetica                       
            ExpC3 = Moeda                                              
            ExpC4 = Descricao da Conta                                 
            ExpC5 = Codigo reduzido                                    
ٱ


/*/
Static Function Ctr400Sint(cConta,cDescSint,cMoeda,cDescConta,cCodRes)

Local aSaveArea := GetArea()

Local lSint    	:= .T.
Local nPosCT1					//Guarda a posicao no CT1
Local cContaPai	:= ""
Local cContaSint	:= ""

dbSelectArea("CT1")
dbSetOrder(1)
If dbSeek(xFilial()+cConta)
	nPosCT1 	:= Recno()
	cDescConta  := &("CT1->CT1_DESC"+cMoeda)
	If Empty(cDescConta)
		cDescConta  := CT1->CT1_DESC01
	Endif
	cCodRes		:= CT1->CT1_RES
	cContaPai	:= CT1->CT1_CTASUP
	If dbSeek(xFilial()+cContaPai)
		cContaSint 	:= CT1->CT1_CONTA
		cDescSint	:= &("CT1->CT1_DESC"+cMoeda)
		If Empty(cDescSint)
			cDescSint := CT1->CT1_DESC01
		Endif
	EndIf	
	dbGoto(nPosCT1)
EndIf	

RestArea(aSaveArea)

Return cContaSint